/* 
	Dump 3 Project
	Weapon: GOH-12 (Get Over Here! 12 gauge)
	Author: Snarboo
*/

actor goh12TractorBeam : LoreShot
/*
	A hacky implementation of a tractor beam using the Lore Shot.
*/
{
	Radius 13
	Height 8
	Speed 64 
	Damage (1) // Must do damage in order to "reel in" enemies!
	ReactionTime 2 // Flies ~128 units before petering out.
	RenderStyle Add
	Alpha 0.75
	Projectile
	SeeSound "weapons/plasmaf"
	ActiveSound ""
	DeathSound "weapons/plasmax"
	Obituary "%o was hooked by %k's tractor beam."
	+PAINLESS // A bit overpowered if it causes pain.
	+RANDOMIZE
	-STRIFEDAMAGE
	States
	{
	Spawn:
		TNT1 A 4 A_Countdown
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}
actor goh12Sequence : Inventory
/*
	This actor keeps track of which barrel has been fired
*/
{
	Inventory.MaxAmount 1
	-INVBAR
}

actor goh12 : Weapon
/*
	Description:
	The GOH-12 is a bog standard 12 gauge with a difference: it has a tractor
	beam!
	
	Weapon Class: Super Shotgun
	
	Primary fire: Semiauto shotgun blast
		-Primary Ammo: Shells
	Secondary: Tractor beam
		-Seconary Ammo: Cells
	Tertiary: When an enemy is close enough, the secondary fires both barrels
		-Tertiary Ammo: Shells
*/
{
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 10
	Weapon.AmmoGive2 20
	Weapon.AmmoType "Shell"
	Weapon.AmmoType2 "Cell"
	Weapon.SlotNumber 3
	Inventory.PickupMessage "You got the GOH-12!"
	Tag "GOH-12"
	// The following flag is necessary for the weapon to behave properly
	// There are still some kinks given the way the weapon works
	+WEAPON.AMMO_CHECKBOTH
	States
	{
	Select:
		GOHR AAAAAA 0 A_Raise
		GOHR A 1 A_Raise 
		Loop
	Deselect:
		GOHR GFEDCBA 1
		GOHR A 0 A_TakeInventory ("goh12Sequence", 0)
			// The barrels are reset when the weapon is lowered
	Deselect2:
		GOHR AAAAAAA 0 A_Lower
		GOHR A 1 A_Lower
		Loop
	Ready:	
		GOHR ABCDEFG 1
		Goto Ready2
	Ready2:
		GOHG A 1 A_WeaponReady
		Loop
	Fire:
		GOLF A 0 A_JumpIfInventory ("Shell", 1, "FireSingle")
			// Does the player have enough shells? If so, fire single barrel
		Goto Ready2 // Else go to idle state
	FireSingle:
		GOLF A 0 A_JumpIfInventory ("goh12Sequence", 1, "FireRight")
			// This keeps track of which barrel has been fired 
		GOLF A 0 A_PlaySound ("weapon/gohfire", CHAN_WEAPON)
		GOLF A 0 A_TakeInventory ("Shell", 1)
		GOLF A 1 Bright A_FireBullets (5, 2, 9, 4, "BulletPuff", 0)
		GOLF A 1 Bright A_SetPitch (pitch - 0.75) 
			// Always remember to reference the player's pitch first!
		GOLF A 1 Bright A_SetPitch (pitch - 0.25)
		GOLF B 1 A_SetPitch (pitch + 0.5)
		GOLF B 1 A_SetPitch (pitch + 0.25)
		GOLF B 1 A_SetPitch (pitch + 0.15)
		GOLF B 1 A_SetPitch (pitch + 0.1)
		GOLF C 3
		GOLF DEF 1
		GOHG A 2 A_GiveInventory ("goh12Sequence", 1)
		GOHG A 0 A_Refire
		Goto Ready2
	FireRight:
		GORF A 0 A_PlaySound ("weapon/gohfire", CHAN_WEAPON)
		GORF A 0 A_TakeInventory ("Shell", 1)
		GORF A 1 Bright A_FireBullets (5, 2, 9, 4, "BulletPuff", 0)
		GORF A 1 Bright A_SetPitch (pitch - 0.5)
		GORF A 1 Bright A_SetPitch (pitch - 0.5)
		GORF B 1 A_SetPitch (pitch + 0.5)
		GORF B 1 A_SetPitch (pitch + 0.25)
		GORF B 1 A_SetPitch (pitch + 0.15)
		GORF B 1 A_SetPitch (pitch + 0.1)
		GORF C 3
		GORF DEF 1
		GOHG A 2 A_TakeInventory ("goh12Sequence", 0)
		GOHG A 0 A_Refire
		Goto Ready2
	AltFire:
		GOTB A 0 A_JumpIfTargetInLOS ("CheckFireBoth", 0, 0, 128)
		GOTB A 0 A_JumpIfInventory ("Cell", 1, "TractorBeam") 
			// Does the player have enough cells? If so, fire tractor beam
		Goto CheckFireBoth // Else fire both barrels
	TractorBeam:
		GOTB A 0 A_JumpIfTargetInLOS ("CheckFireBoth", 0, 0, 112)
		GOTB A 2 Bright A_RailAttack (0, 0, 0, "Blue", "None", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB A 0 A_FireCustomMissile ("goh12TractorBeam", 0, 1)
		GOTB B 0 A_JumpIfTargetInLOS ("CheckFireBoth", 0, 0, 112)
		GOTB B 2 Bright A_RailAttack (0, 0, 0, "Blue", "None", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB B 0 A_FireCustomMissile ("goh12TractorBeam", 0, 0)
		GOTB C 0 A_JumpIfTargetInLOS ("CheckFireBoth", 0, 0, 112)
		GOTB C 2 Bright A_RailAttack (0, 0, 0, "Blue", "None", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB C 0 A_FireCustomMissile ("goh12TractorBeam", 0, 0)
		GOTB D 0 A_JumpIfTargetInLOS ("CheckFireBoth", 0, 0, 112)
		GOTB D 2 Bright A_RailAttack (0, 0, 0, "Blue", "None", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB D 0 A_FireCustomMissile ("goh12TractorBeam", 0, 0)
		Goto Ready2
	CheckFireBoth:
		GOHG A 0 A_JumpIfInventory ("Shell", 2, "FireBoth")
			// Are there enough shells? If so, fire both barrels
		Goto Fire // Else fire single barrel
	FireBoth:
		GOBF A 0 A_PlaySound ("weapon/gohfire2", CHAN_WEAPON)
		GOBF A 0 A_TakeInventory ("Shell", 2)
		GOBF A 1 Bright A_FireBullets (6, 3, 18, 4, "BulletPuff", 0)
		GOBF A 1 Bright A_SetPitch (pitch - 1.25)
		GOBF A 1 Bright A_SetPitch (pitch - 0.75)
		GOBF B 1 A_SetPitch (pitch - 0.5)
		GOBF B 1 A_SetPitch (pitch + 1.50)
		GOBF B 1 A_SetPitch (pitch + 0.75)
		GOBF B 1 A_SetPitch (pitch + 0.25)
		GOBF C 4
		GOBF DEFGH 1
		GOHG A 20
		GOHG A 5 A_Refire ("AltFire")
		Goto Ready2
	Spawn:
		PLAS A -1
		Stop
	}
}