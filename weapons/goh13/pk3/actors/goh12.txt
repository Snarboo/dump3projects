/* 
	Dump 3 Project
	Weapon: GOH-12 (Get Over Here! 12 gauge)
	Author: Snarboo
*/

actor tractorBeam : LoreShot
/*
	A hacky implementation of a tractor beam using the Lore Shot.
*/
{
	Radius 13
	Height 8
	Speed 64 
	Damage (1) // Must do damage in order to "reel in" enemies!
	ReactionTime 2 // Flies ~128 units before petering out.
	RenderStyle Add
	Alpha 0.75
	Projectile
	SeeSound "weapons/plasmaf"
	ActiveSound ""
	DeathSound "weapons/plasmax"
	Obituary "%o was hooked by %k's tractor beam."
	+PAINLESS // A bit overpowered if it causes pain.
	+RANDOMIZE
	-STRIFEDAMAGE
	States
	{
	Spawn:
		TNT1 A 4 A_Countdown
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}
actor goh12Sequence : Inventory
/*
	This actor keeps track of which barrel has been fired
*/
{
	Inventory.MaxAmount 1
	-INVBAR
}

actor goh12 : Weapon
/*
	Description:
	The GOH-12 is a bog standard 12 gauge with a difference: it has a tractor
	beam!
	
	Weapon Class: ????
	
	Primary fire: Semiauto shotgun blast
		-Primary Ammo: Shells
	Secondary: Tractor beam
		-Seconary Ammo: Cells
	Tertiary: When an enemy is close enough, the secondary fires both barrels
		-Tertiary Ammo: Shells
*/
{
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 10
	Weapon.AmmoGive2 40
	Weapon.AmmoType "Shell"
	Weapon.AmmoType2 "Cell"
	Weapon.SlotNumber 6
	Inventory.PickupMessage "You got the GOH-12!"
	Tag "GOH-12"
	States
	{
	Select:
		GOHR AAAAAA 0 A_Raise
		GOHR A 1 A_Raise 
		Loop
	Deselect:
		GOHR GFEDCBA 1
		GOHR A 0 A_TakeInventory ("goh12Sequence", 0)
	Deselect2:
		GOHR AAAAAAA 0 A_Lower
		GOHR A 1 A_Lower
		Loop
	Ready:	
		GOHR ABCDEFG 1
		Goto Ready2
	Ready2:
		GOHG A 1 A_WeaponReady
		Loop
	Fire:
		GOLF A 0 A_JumpIfInventory ("goh12Sequence", 1, "FireRight")
		GOLF A 1 Bright A_FireShotgun
		GOLF A 1 Bright A_SetPitch (-0.75)
		GOLF A 1 Bright A_SetPitch (-0.25)
		GOLF B 1 A_SetPitch (+0.5)
		GOLF B 1 A_SetPitch (+0.25)
		GOLF B 1 A_SetPitch (+0.15)
		GOLF B 1 A_SetPitch (+0.1)
		GOLF C 3
		GOLF DEF 1
		GOHG A 2 A_GiveInventory ("goh12Sequence", 1)
		GOHG A 0 A_Refire
		Goto Ready2
	FireRight:
		GORF A 1 Bright A_FireShotgun
		GORF A 1 Bright A_SetPitch (-0.5)
		GORF A 1 Bright A_SetPitch (-0.5)
		GORF B 1 A_SetPitch (+0.5)
		GORF B 1 A_SetPitch (+0.25)
		GORF B 1 A_SetPitch (+0.15)
		GORF B 1 A_SetPitch (+0.1)
		GORF C 3
		GORF DEF 1
		GOHG A 2 A_TakeInventory ("goh12Sequence", 0)
		GOHG A 0 A_Refire
		Goto Ready2
	AltFire:
		GOTB A 0 A_JumpIfTargetInLOS ("AltHold2", 0, 0, 128)
		GOTB A 0 A_Refire
		Goto Ready2
	AltHold:
		GOTB A 2 Bright A_RailAttack (0, 0, 0, "None", "Blue", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB A 0 A_FirePlasma
		GOTB A 0 A_JumpIfTargetInLOS ("AltHold2", 0, 0, 112)
		GOTB B 2 Bright A_RailAttack (0, 0, 0, "None", "Blue", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB B 0 A_FirePlasma
		GOTB B 0 A_JumpIfTargetInLOS ("AltHold2", 0, 0, 112)
		GOTB C 2 Bright A_RailAttack (0, 0, 0, "None", "Blue", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB C 0 A_FirePlasma
		GOTB C 0 A_JumpIfTargetInLOS ("AltHold2", 0, 0, 112)
		GOTB D 2 Bright A_RailAttack (0, 0, 0, "None", "Blue", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		GOTB D 0 A_FirePlasma
		GOTB D 0 A_JumpIfTargetInLOS ("AltHold2", 0, 0, 112)
		Goto Ready2
	AltHold2:
		GOBF A 0 Bright A_FireShotgun
		GOBF A 1 Bright A_FireShotgun
		GOBF A 1 Bright A_SetPitch (-1.25)
		GOBF A 1 Bright A_SetPitch (-0.75)
		GOBF B 1 A_SetPitch (-0.5)
		GOBF B 1 A_SetPitch (+1.50)
		GOBF B 1 A_SetPitch (+0.75)
		GOBF B 1 A_SetPitch (+0.25)
		GOBF C 4
		GOBF DEFGH 1
		GOHG A 5 A_Refire ("AltFire")
		Goto Ready2
	Spawn:
		PLAS A -1
		Stop
	}
}