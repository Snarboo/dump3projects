/* 
	Dump 3 Project
	Weapon: GOH-12 (Get Over Here! 12 gauge)
	Author: Snarboo
*/

actor tractorBeam : LoreShot replaces PlasmaBall
/*
	A hacky implementation of a tractor beam using the Lore Shot.
*/
{
	Radius 13
	Height 8
	Speed 64 
	Damage (1) // Must do damage in order to "reel in" enemies!
	ReactionTime 2 // Flies ~128 units before petering out.
	RenderStyle Add
	Alpha 0.75
	Projectile
	SeeSound "weapons/plasmaf"
	ActiveSound ""
	DeathSound "weapons/plasmax"
	Obituary "%o was hooked by %k's tractor beam."
	+PAINLESS // A bit overpowered if it causes pain.
	+RANDOMIZE
	-STRIFEDAMAGE
	States
	{
	Spawn:
		TNT1 A 4 A_Countdown
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR goh12 : PlasmaRifle replaces PlasmaRifle
/*
	Description:
	The GOH-12 is a bog standard 12 gauge with a difference: it has a tractor
	beam!
	
	Weapon Class: ????
	
	Primary fire: Semiauto shotgun blast
		-Primary Ammo: Shells
	Secondary: Tractor beam
		-Seconary Ammo: Cells
	Tertiary: When an enemy is close enough, the secondary fires both barrels
		-Tertiary Ammo: Shells
*/
{
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 10
	Weapon.AmmoGive2 40
	Weapon.AmmoType "Shell"
	Weapon.AmmoType2 "Cell"
	Weapon.SlotNumber 6
	States
	{
	Fire:
		PLSG A 3 A_FireShotgun
		PLSG B 15
		PLSG B 0 A_Refire
		Goto Ready
	Hold2:
		PLSG A 0 A_FireShotgun
		PLSG A 3 A_FireShotgun
		PLSG B 30
		PLSG B 0 A_Refire ("AltFire")
		Goto Ready
	AltFire:
		PLSG A 0 A_JumpIfTargetInLOS ("Hold2", 0, 0, 128)
		PLSG A 0 A_Refire
		Goto Ready
	AltHold:
		PLSG A 2 A_RailAttack (0, 0, 0, "None", "Blue", RGF_Silent | RGF_Fullbright | RGF_NoPiercing, 0, "None", 0, 0, 128, 2)
		PLSG A 0 A_FirePlasma
		PLSG A 0 A_JumpIfTargetInLOS ("Hold2", 0, 0, 112)
		Goto Ready
	}
}